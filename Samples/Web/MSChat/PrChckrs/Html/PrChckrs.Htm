<HTML>
<SCRIPT LANGUAGE="VBScript">
<!--
'##########################################################################
'#Global Variables
'##########################################################################
Option Explicit

Dim strSelectParticipants          'Participants that are selected, comma separated
Dim rgstrSelectParticipants(19)    'The array of recipients for whispering
Dim strRecipients                  'Recipients of an incoming message
Dim lHistoryLen                    'Current number of characters in the history window
Dim bInitialMembers                'Is the initial member list being exposed?

'##########################################################################
'#User Defined Subs
'##########################################################################
Sub AppendHistory(strText, nIndent, nCR)

  Dim i    
  Dim lSelStart
  Dim iSelLength
  Dim strIndented
    
  lSelStart = document.WEBChat.rtfHistory.SelStart
  iSelLength = document.WEBChat.rtfHistory.SelLength
    
  If (nCR > 0 And document.WEBChat.rtfHistory.Text <> "") Then
    For i = 1 To nCR
      document.WEBChat.rtfHistory.SelStart = lHistoryLen
      document.WEBChat.rtfHistory.SelLength = 0
      document.WEBChat.rtfHistory.SelText = vbCrLf
      lHistoryLen = lHistoryLen + 2
    Next
  End If

  strIndented = Space(nIndent*2) + strText
  
  document.WEBChat.rtfHistory.SelStart = lHistoryLen
  document.WEBChat.rtfHistory.SelLength = 0
  document.WEBChat.rtfHistory.SelText = strIndented  
  lHistoryLen = lHistoryLen + Len(strIndented)
        
  If (iSelLength > 0) Then
    document.WEBChat.rtfHistory.SelStart = lSelStart
    document.WEBChat.rtfHistory.SelLength = iSelLength
  Else
    document.WEBChat.rtfHistory.SelStart = lHistoryLen
    document.WEBChat.rtfHistory.SelLength = 0
  End If
End Sub

Function VPropertyFromChatItems(ci, strPropertyName)
  If (ci.ItemValid(strPropertyName)) Then
    VPropertyFromChatItems = ci.Item(strPropertyName)
  Else
    VPropertyFromChatItems = Empty
  End If
End Function

Sub FillRecipients
  Dim MyList
  Dim i,j
    
  For i = 0 To 19
    rgstrSelectParticipants(i) = ""
  Next

  strSelectParticipants = ""
  Set MyList = document.WEBChat.lstParticipants

  For i = 0 To MyList.ListCount-1
    If MyList.Selected(i) And MyList.List(i) <> MsChatPr1.Nickname Then
      If strSelectParticipants = "" Then
        strSelectParticipants = MyList.List(i)
        rgstrSelectParticipants(0) = MyList.List(i)
        j = 1
      Else
        If j > 19 Then
          MsgBox "Whispers are limited to 20 participants."
          Exit Sub
        End If
        strSelectParticipants = strSelectParticipants + ", "
        strSelectParticipants = strSelectParticipants + MyList.List(i)
        rgstrSelectParticipants(j) = MyList.List(i)
        j = j + 1
      End If
    End If
  Next
End Sub

Sub ExtractRecipients(RecipientNicknames)
  strRecipients = ""
  If (Not IsEmpty(RecipientNicknames)) Then
    If (VarType(RecipientNicknames) = vbString) Then
      strRecipients = CStr(RecipientNicknames)
    Else
      Dim i
      strRecipients = CStr(RecipientNicknames(LBound(RecipientNicknames)))
      For i = LBound(RecipientNicknames) + 1 To UBound(RecipientNicknames)
        strRecipients = strRecipients + ", " + CStr(RecipientNicknames(i))
      Next
    End If
  End If
End Sub

'##########################################################################
'# Form Events
'##########################################################################
Sub Window_OnLoad()
  document.WEBChat.rtfHistory.HideSelection = FALSE
  document.WEBChat.rtfHistory.Locked = TRUE
  document.WEBChat.lstParticipants.IntegralHeight = FALSE
  bInitialMembers = FALSE
  PlayBoard1.Size = 8
  MsChatPr1.Channels.Add
End Sub

Sub Window_OnUnload()
  Select Case MsChatPr1.ConnectionState
    Case 1,3:
      MsChatPr1.CancelConnectLogin

    Case 2,4:
      MsChatPr1.Disconnect
  End Select
End Sub


'##########################################################################
'# UI OCX Events
'##########################################################################
Sub Connect_Click()
  Select Case MsChatPr1.ConnectionState
    Case 0, 2:
      If document.WEBChat.txtServer.Value <> "" Then
        MsChatPr1.Login document.WEBChat.txtServer.Value, document.WEBChat.txtNickName.Value, ,document.WEBChat.txtRealName.Value
      Else
        MsgBox "Must first enter a server to connect to."
      End If

    Case 1, 3:
      MsChatPr1.CancelConnectLogin

    Case 4:
      MsChatPr1.Disconnect
      lHistoryLen = 0
      document.WEBChat.lblNumPart.Caption = "# of participants: None"
      document.WEBChat.rtfHistory.Text = ""
      document.WEBChat.lstParticipants.Clear
  End Select
End Sub

Sub btnGo_Click()
  If MsChatPr1.ConnectionState <> 4 Then
    MsgBox "Must first be connected to a server"
    Exit Sub
  End If

  If document.WEBChat.btnGo.Value = "Join" Then
    MsChatPr1.Channels(1).JoinChannel document.WEBChat.txtRoomName.Value
  Else
    MsChatPr1.Channels(1).LeaveChannel
  End If
End Sub

Sub btnSend_Click()
  If document.WEBChat.rtfMessage.Text = "" Then
    MsgBox "You must first enter text before speaking."
    Exit Sub
  End If
  If MsChatPr1.Channels(1).ChannelState = 2 Then
    MsChatPr1.Channels(1).SendMessage 0, document.WEBChat.rtfMessage.Text
    AppendHistory MsChatPr1.Nickname + " says: ", 0, 1
    AppendHistory document.WEBChat.rtfMessage.Text, 1, 1
    document.WEBChat.rtfMessage.Text = ""
  Else
    MsgBox "You are not in a room."
  End If
End Sub

Sub btnWhisper_Click()
  If document.WEBChat.rtfMessage.Text = "" Then
    MsgBox "You must first enter text before whispering."
    Exit Sub
  End If

  FillRecipients

  If MsChatPr1.Channels(1).ChannelState = 2 Then
    If strSelectParticipants = "" Then
      MsgBox "You must select recipient(s) other than yourself before whispering."
    Else
      Select Case MsChatPr1.ConnectionType
        Case 0:
          MsChatPr1.SendPrivateMessage 0, document.WEBChat.rtfMessage.Text, rgstrSelectParticipants
        Case 1:
          MsChatPr1.Channels(1).SendMessage 32, document.WEBChat.rtfMessage.Text, rgstrSelectParticipants
      End Select
      AppendHistory MsChatPr1.Nickname + " whispers to " + strSelectParticipants + ": ", 0, 1
      AppendHistory document.WEBChat.rtfMessage.Text, 1, 1
      document.WEBChat.rtfMessage.Text = ""
    End If
  Else
    MsgBox "You are not in a room."
  End If
End Sub

Sub btnResetGame_Click()
  If document.frmResetGame.txtGameMaster.Value = MsChatPr1.Nickname Or document.frmResetGame.txtGameMaster.Value = "" Then
    PlayBoard1.ResetCheckers
    If document.frmResetGame.txtGameMaster.Value = MsChatPr1.Nickname Then
	  MsChatPr1.Channels(1).SendMessage 2, " ", , "cgResetCheckers"
	End If
  Else
    MsgBox "Only the master of the game, " + document.frmResetGame.txtGameMaster.Value + ", can reset the game."
  End If
End Sub


'##########################################################################
'# MSChat Protocol OCX Events
'##########################################################################
Sub MsChatPr1_OnAddMember(Channel, Nickname, MemberItems)
  document.WEBChat.lstParticipants.AddItem Nickname
  document.WEBChat.lblNumPart.Caption = "# of participants: " + CStr(MsChatPr1.Channels(1).MemberCount)
  If (Not bInitialMembers) Then
    AppendHistory Nickname + " joined the conversation.", 0, 1
    If document.frmResetGame.txtGameMaster.Value = MsChatPr1.Nickname Then
      'I'm the master and need to propagate the current game
	  Dim vCheckerPos
	  vCheckerPos = PlayBoard1.CheckerPositions(TRUE)
	  'MsgBox "Sending the White positions"
	  'MsgBox "Vartype(vCheckerPos) = " + CStr(Vartype(vCheckerPos))
	  'MsgBox "LBound(vCheckerPos) = " + CStr(LBound(vCheckerPos))
	  'MsgBox "UBound(vCheckerPos) = " + CStr(UBound(vCheckerPos))
	  'MsgBox "vCheckerPos(1) = " + CStr(vCheckerPos(1))
	  'MsgBox "vCheckerPos(2) = " + CStr(vCheckerPos(2))
	  'MsgBox "vCheckerPos(3) = " + CStr(vCheckerPos(3))
	  'MsgBox "vCheckerPos(4) = " + CStr(vCheckerPos(4))
	  Channel.SendMessage 2, vCheckerPos, Nickname, "cgWhitePos"
	  vCheckerPos = PlayBoard1.CheckerPositions(FALSE)
	  Channel.SendMessage 2, vCheckerPos, Nickname, "cgBlackPos"
    End If
  End If
  If Nickname > document.frmResetGame.txtGameMaster.Value Then
	document.frmResetGame.txtGameMaster.Value = Nickname
  End If
End Sub

Sub MsChatPr1_OnBeginEnumeration(Channel, EnumType)
End Sub

Sub MsChatPr1_OnChannelError(Channel, ErrorCode, Description)
  MsgBox "OnChannelError: " + Description + ", Err: " + CStr(ErrorCode)
End Sub

Sub MsChatPr1_OnChannelPropertyChanged(Channel, ModifierNickname, ChannelPropertyName, OldChannelProperty, NewChannelProperty)
  If CStr(OldChannelProperty) = "" Then
    MsgBox ModifierNickName + " has changed the " + ChannelPropertyName + " to " + CStr(NewChannelProperty)
  Else
    MsgBox ModifierNickName + " has changed the " + ChannelPropertyName + " from " + CStr(OldChannelProperty) + " to " + CStr(NewChannelProperty)
  End If
End Sub

Sub MsChatPr1_OnChannelState(ByVal Channel, ByVal NewChannelState)
  Select Case NewChannelState
    Case 0:
      document.WEBChat.btnGo.Value = "Join"
      document.WEBChat.lblRoomStatus.Caption = "Not in a room"
	  document.frmResetGame.txtGameMaster.Value = ""

    Case 1:
      document.WEBChat.btnGo.Value = "Wait..."
      document.WEBChat.lblRoomStatus.Caption = "Entering a room..."

    Case 2:
      document.WEBChat.btnGo.Value = "Leave"
      document.WEBChat.lblRoomStatus.Caption = "Now chatting in room " + MsChatPr1.Channels(1).ChannelName
      bInitialMembers = TRUE
  End Select
End Sub

Sub MsChatPr1_OnConnectionError(ErrorCode, Description, ChannelName)
  If ErrorCode = 3133 Then
    Exit Sub
  End If

  Dim strDisplay

  Select Case VarType(ChannelName)
    Case vbEmpty:
      strDisplay = "OnConnectionError: " + Description + ", Err: " + CStr(ErrorCode)

    Case vbString:
      strDisplay = "OnConnectionError CN: " + ChannelName + ", " + Description + ", Err: " + CStr(ErrorCode)
  End Select

  MsgBox strDisplay
End Sub

Sub MsChatPr1_OnConnectionState(NewConnectionState)
  Select Case NewConnectionState
    Case 0:
      document.WEBChat.btnConnect.Value = "Connect"
      document.WEBChat.lblConnectionState.Caption = "**Disconnected**"
      document.WEBChat.lblConnectionState.BackColor = "255"

    Case 1:
      document.WEBChat.btnConnect.Value = "Cancel"
      document.WEBChat.lblConnectionState.Caption = "Connecting..."
      document.WEBChat.lblConnectionState.BackColor = "65535"

    Case 2:
      document.WEBChat.btnConnect.Value = "Disconnect"
      document.WEBChat.lblConnectionState.Caption = "Connected"
      document.WEBChat.lblConnectionState.BackColor = "65535"

    Case 3:
      document.WEBChat.btnConnect.Value = "Cancel"
      document.WEBChat.lblConnectionState.Caption = "Logging..."
      document.WEBChat.lblConnectionState.BackColor = "65535"

    Case 4:
      document.WEBChat.btnConnect.Value = "Disconnect"
      document.WEBChat.lblConnectionState.Caption = "**Logged**"
      document.WEBChat.lblConnectionState.BackColor = "65280"

    Case 5:
      document.WEBChat.btnConnect.Value = "Wait..."
      document.WEBChat.lblConnectionState.Caption = "Disconnecting"
      document.WEBChat.lblConnectionState.BackColor = "255"
  End Select
End Sub

Sub MsChatPr1_OnDelMember(Channel, Nickname)
  Dim MyList
  Dim X

  Set MyList = document.WEBChat.lstParticipants

  If Nickname <> MsChatPr1.Nickname Then
    document.WEBChat.lblNumPart.Caption = "# of participants: " + CStr(MsChatPr1.Channels(1).MemberCount)
    AppendHistory Nickname + " left the conversation.", 0, 1
  Else
    document.WEBChat.lblNumPart.Caption = "# of participants: None"
    document.WEBChat.rtfHistory.Text = ""
    lHistoryLen = 0
    MyList.Clear
    Exit Sub
  End If

  X = 0
  Do While Nickname <> (MyList.List(X))
    X = X + 1
  Loop
  MyList.RemoveItem X

  If Nickname = document.frmResetGame.txtGameMaster.Value Then
    'We lost our master
	document.frmResetGame.txtGameMaster.Value = ""
    'Define the new master
	Dim ci 
	Set ci = MsChatPr1.NewChatItems
	Channel.ListMembers ci
	Set ci = Nothing
  End If
End Sub

Sub MsChatPr1_OnEndEnumeration(Channel, EnumType)
  If EnumType = 5 Then
    bInitialMembers = FALSE
  End If
End Sub

Sub MsChatPr1_OnMemberKicked(Channel, KickedNickname, KickerNickname, Reason)
  If Reason = "" Then
    MsgBox KickerNickname + " kicked " + KickedNickname + " out of the chat room."
  Else
    MsgBox KickerNickname + " kicked " + KickedNickname + " out of the chat room, saying '" + Reason + "'."
  End If
End Sub

Sub MsChatPr1_OnMemberProperty(Channel, MemberItems)
  Dim strNickname
  strNickname = MemberItems.Item("Nickname")
  If strNickname > document.frmResetGame.txtGameMaster.Value Then
	document.frmResetGame.txtGameMaster.Value = strNickname
  End If
End Sub

Sub MsChatPr1_OnMemberPropertyChanged(Channel, ModifiedNickname, ModifierNickname, MemberPropertyName, OldMemberProperty, NewMemberProperty)
  Dim MyList
  Dim MyListCount
  Dim X

  If MemberPropertyName = "Nickname" Then
    Set MyList = document.WEBChat.lstParticipants
    MyListCount = MyList.ListCount
    X = 0
    Do While X < MyListCount
      If OldMemberProperty = (MyList.List(X)) then
        MyList.List(X) = NewMemberProperty
        X = MyListCount
      End If
      X = X + 1
    Loop
    If OldMemberProperty = document.frmResetGame.txtGameMaster.Value Then
      'We lost our master
	  document.frmResetGame.txtGameMaster.Value = ""
      'Define the new master
	  Dim ci
	  Set ci = MsChatPr1.NewChatItems
	  Channel.ListMembers ci
	  Set ci = Nothing
    End If
    MsgBox OldMemberProperty + " is now known as " + NewMemberProperty + "."
  End If
End Sub

Sub MsChatPr1_OnMessage(Channel, SenderNickname, MessageType, Message, RecipientNicknames, DataMessageTag)
  Select Case MessageType
    Case 0:
      AppendHistory SenderNickname + " says:", 0, 1

    Case 1:
      AppendHistory SenderNickname + " says (as a notice):", 0, 1

    Case 32:
	  ExtractRecipients RecipientNicknames
      AppendHistory SenderNickname + " whispers to " + strRecipients + ":", 0, 1

    Case 128:
      AppendHistory SenderNickname + " " + Message, 0, 1
    
    Case 256:
      AppendHistory SenderNickname + " sends sound:", 0, 1
        
    Case 512:
      AppendHistory SenderNickname + " sends CTCP:", 0, 1

    Case 2:
      If SenderNickname = document.frmResetGame.txtGameMaster.Value Then
	    Select Case DataMessageTag
          Case "cgResetCheckers":
            PlayBoard1.ResetCheckers
	    End Select
      End If
      Select Case DataMessageTag
	    Case "cgWhitePos":
		  'MsgBox "Got the White positions"
		  'MsgBox "Vartype(Message) = " + CStr(Vartype(Message))
		  'MsgBox "LBound(Message) = " + CStr(LBound(Message))
		  'MsgBox "UBound(Message) = " + CStr(UBound(Message))
		  'MsgBox "Message(1) = " + CStr(Message(1))
		  'MsgBox "Message(2) = " + CStr(Message(2))
		  'MsgBox "Message(3) = " + CStr(Message(3))
		  'MsgBox "Message(4) = " + CStr(Message(4))
	      PlayBoard1.CheckerPositions(TRUE) = Message
	    Case "cgBlackPos":
	      PlayBoard1.CheckerPositions(FALSE) = Message
		Case "cgCheckerMove":
		  PlayBoard1.MoveChecker CInt(Message(1)), CInt(Message(2)), CInt(Message(3)), CInt(Message(4))
	  End Select
  End Select

  Select Case MessageType
    Case 0, 1, 32, 256, 512:
      AppendHistory Message, 1, 1
  End Select
End Sub

Sub MsChatPr1_OnPrivateMessage(SenderNickname, PrivateMessageType, Message, DataMessageTag)
  Select Case PrivateMessageType
    Case 0, 1, 32, 512, 513, 1024, 1025, 2048, 2049:
      AppendHistory SenderNickname + " sends private message:", 2, 1
      AppendHistory Message, 3, 1
	Case 64:
      AppendHistory SenderNickname + " asked for version information.", 2, 1
	Case 128:
      AppendHistory SenderNickname + " asked for lag time.", 2, 1
	Case 256:
      AppendHistory SenderNickname + " asked for local time.", 2, 1
  End Select
End Sub

Sub MsChatPr1_OnProtocolMessage(MessagePrefix, MessageCommand, MessageParameters, EnableDefault)
  Dim i
  Dim strParams

  strParams = ""
  Select Case VarType(MessageParameters)
    Case 8:
      strParams = CStr(MessageParameters)
    Case 8204:
      For i = LBound(MessageParameters) To UBound(MessageParameters)
        strParams = strParams + "<" + CStr(MessageParameters(i)) + "> "
      Next
  End Select
End Sub

Sub MsChatPr1_OnServerTextMessage(ServerMessageType, Text)
End Sub


'##########################################################################
'# PlayBoard OCX Events
'##########################################################################
Sub PlayBoard1_OnCheckerMoved(FromMoveCheckerCall, XO, YO, XD, YD)
  If Not FromMoveCheckerCall And MsChatPr1.Channels(1).ChannelState = 2 Then
    Dim vCheckerMove(4)
    vCheckerMove(0) = 0	'VB Script does not allow arrays with LBound <> 0
    vCheckerMove(1) = CInt(XO)
    vCheckerMove(2) = CInt(YO)
    vCheckerMove(3) = CInt(XD)
    vCheckerMove(4) = CInt(YD)
    MsChatPr1.Channels(1).SendMessage 2, vCheckerMove, , "cgCheckerMove"
  End If
End Sub

-->
</SCRIPT>

   <HEAD><TITLE>MsChatPr Sample Page - Checker Game</TITLE></HEAD>
   <BODY BGCOLOR=000000 TEXT=#FFFFFF LINK=#000000 VLINK=#008080>

   <FORM NAME="WEBChat"><CENTER><FONT SIZE=+3>- MsChatPr Sample Page - Checker Game -</FONT></CENTER>
   <P><TABLE BORDER=0 CELLPADDING=5 CELLSPACING=0 WIDTH=600>
	<TR>
	  <TD>Real Name:</TD>
	  <TD><INPUT TYPE=TEXT VALUE="James Bond" SIZE=30 NAME="txtRealName" MAXLENGTH=58></TD>
	  <TD>Server Name:</TD>
	  <TD><INPUT TYPE=TEXT VALUE="chat.microsoft.com" SIZE=30 NAME="txtServer"></TD>
	</TR>

	<TR>
	  <TD>Nickname:</TD>
	  <TD><INPUT TYPE=TEXT VALUE="JB007" SIZE=30 NAME="txtNickName" MAXLENGTH=58></TD>
	  <TD></TD>
	  <TD><INPUT TYPE=BUTTON VALUE="Connect" onClick="Connect_Click" NAME="btnConnect"></TD>
	</TR>

	<TR>
	  <TD COLSPAN=4><HR></TD>
	</TR>

	<TR>
	  <TD>Room Name:</TD>
	  <TD><INPUT TYPE=TEXT VALUE="#CheckerGame" SIZE=30 NAME="txtRoomName" MAXLENGTH=58></TD>
	  <TD><INPUT TYPE=BUTTON VALUE="Join" NAME="btnGo" onClick="btnGo_Click"></TD>
	</TR>
   </TABLE>

   <P><CENTER><TABLE BORDER=1 CELLSPACING=0 CELLPADDING=0 WIDTH=600 BGCOLOR=FFFFFF>
	<TR>
	  <TD COLSPAN=5 ALIGN=LEFT><OBJECT ID="lblConnectionState" WIDTH=85 HEIGHT=16
 	  CLASSID="CLSID:978C9E23-D4B0-11CE-BF2D-00AA003F40D0">
		<PARAM NAME="VariousPropertyBits" VALUE="8388627">
		<PARAM NAME="Caption" VALUE="**Disconnected**">
		<PARAM NAME="BackColor" VALUE="255">
		<PARAM NAME="Size" VALUE="3620;418">
		<PARAM NAME="FontCharSet" VALUE="0">
		<PARAM NAME="FontPitchAndFamily" VALUE="2">
		<PARAM NAME="FontWeight" VALUE="0">
	  </OBJECT>**<OBJECT ID="lblNumPart" WIDTH=137 HEIGHT=16
 	  CLASSID="CLSID:978C9E23-D4B0-11CE-BF2D-00AA003F40D0">
		<PARAM NAME="VariousPropertyBits" VALUE="8388627">
		<PARAM NAME="Caption" VALUE="# of participants: None">
		<PARAM NAME="Size" VALUE="3620;418">
		<PARAM NAME="FontCharSet" VALUE="0">
		<PARAM NAME="FontPitchAndFamily" VALUE="2">
		<PARAM NAME="FontWeight" VALUE="0">
	  </OBJECT>**<OBJECT ID="lblRoomStatus" WIDTH=200 HEIGHT=16
 	  CLASSID="CLSID:978C9E23-D4B0-11CE-BF2D-00AA003F40D0">
		<PARAM NAME="VariousPropertyBits" VALUE="8388627">
		<PARAM NAME="Caption" VALUE="Not in a room">
		<PARAM NAME="Size" VALUE="3620;418">
		<PARAM NAME="FontCharSet" VALUE="0">
		<PARAM NAME="FontPitchAndFamily" VALUE="2">
		<PARAM NAME="FontWeight" VALUE="0">
	  </OBJECT></TD>
	</TR>
	<TR ROWSPAN=3>
	  <TD COLSPAN=4><OBJECT ID="rtfHistory" WIDTH=465 HEIGHT=125
 		CLASSID="CLSID:8BD21D10-EC42-11CE-9E0D-00AA006002F3">
		<PARAM NAME="VariousPropertyBits" VALUE="2894088219">
		<PARAM NAME="ScrollBars" VALUE="2">
		<PARAM NAME="Size" VALUE="11187;3175">
		<PARAM NAME="FontCharSet" VALUE="0">
		<PARAM NAME="FontPitchAndFamily" VALUE="2">
		<PARAM NAME="FontWeight" VALUE="0">
	  </OBJECT></TD>
	  <TD ROWSPAN=2><OBJECT ID="lstParticipants" WIDTH=150 HEIGHT=125
	  CLASSID="CLSID:8BD21D20-EC42-11CE-9E0D-00AA006002F3">
	        <PARAM NAME="ScrollBars" VALUE="2">
		<PARAM NAME="DisplayStyle" VALUE="2">
		<PARAM NAME="Size" VALUE="15999;2720">
		<PARAM NAME="MatchEntry" VALUE="0">
		<PARAM NAME="MultiSelect" VALUE="2">
		<PARAM NAME="FontCharSet" VALUE="0">
		<PARAM NAME="FontPitchAndFamily" VALUE="2">
		<PARAM NAME="FontWeight" VALUE="0">
	  </OBJECT><BR>
	  <INPUT TYPE=BUTTON VALUE="Send" NAME="btnSend" onClick="btnSend_Click"><INPUT TYPE=BUTTON VALUE="Whisper" NAME="btnWhisper" onClick="btnWhisper_Click"></TD>
	</TR>

	<TR>
	  <TD COLSPAN=4><OBJECT ID="rtfMessage" WIDTH=465 HEIGHT=20
 		CLASSID="CLSID:8BD21D10-EC42-11CE-9E0D-00AA006002F3">
		<PARAM NAME="ScrollBars" VALUE="2">
		<PARAM NAME="Size" VALUE="11187;5715">
		<PARAM NAME="FontCharSet" VALUE="0">
		<PARAM NAME="FontPitchAndFamily" VALUE="2">
		<PARAM NAME="FontWeight" VALUE="0">
	  </OBJECT></TD>
	</TR>
   </TABLE></CENTER>

   <CENTER><FORM NAME="frmResetGame"><TABLE BORDER=0 CELLSPACING=5 CELLPADDING=0 WIDTH=600>
	<TR>
	  <TD><BR><INPUT TYPE=BUTTON VALUE="Reset Game" OnClick="btnResetGame_Click" NAME="btnResetGame">
			  Game Master:<INPUT TYPE=TEXT VALUE="" SIZE=20 NAME="txtGameMaster" MAXLENGTH=60>
	  </FORM></TD>
	</TR>

	<TR>
	  <TD><BR><OBJECT ID="PlayBoard1" WIDTH=275 HEIGHT=200
		STANDBY="Dowloading the PlayBoard ActiveX Control" 
		CODETYPE="application/x-oleobject"
		CODEBASE="PlayBd.Ocx"
                CLASSID="CLSID:1A177811-1234-11D1-99CC-00C04FD64497">
    </OBJECT></TD>
	</TR>
   </TABLE></CENTER>

	<OBJECT ID="MsChatPr1" WIDTH=0 HEIGHT=1
	STANDBY="Dowloading the Microsoft Chat Protocol Control" 
	CODETYPE="application/x-oleobject"
	CODEBASE="MsChatPr.Cab"
	CLASSID="CLSID:039397C0-46DF-11D0-99CB-00C04FD64497">
	</OBJECT>

   <P><CENTER><FONT SIZE=-1>

   <SCRIPT LANGUAGE="VBScript">
   </SCRIPT>
   </FONT></CENTER>
</BODY>
</HTML>
